IF
	Die()
	Global("CB_RynnWithPC","GLOBAL",0)
	!StateCheck(Myself,STATE_STONE_DEATH)
	OR(2)
		Allegiance(Myself,ENEMY)
		Allegiance(Myself,NEUTRAL)
	Global("t#RYNNbody","LOCALS",0)
THEN
	RESPONSE #100
		CreateItem("cbrynn",0,0,0)
		SetGlobal("t#RYNNbody","LOCALS",1)
		DropInventory()
END

IF
Global("CB_RynnWithPC","GLOBAL",0)
Global("CB_RynnSleep","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("CB_RynnSleep","LOCALS",1)
GiveItemCreate("minhp1",Myself,1,0,0)
FillSlot(SLOT_AMULET)
END

IF
	Global("CbRynnsQuest","GLOBAL",6)
	Global("CbTimeToGo","LOCALS",0)
THEN
	RESPONSE #100
		SetDialog("RYNN")
		StartDialogueNoSet(Player1)
END

IF
	Global("CbRynnsQuest","GLOBAL",7)
	GlobalGT("SPRITE_IS_DEADCBYT4446","GLOBAL",27)
	!AreaCheck("AR3644")
	!AreaCheck("AR3645")
	!AreaCheck("AR3646")
	CombatCounter(0)
THEN
	RESPONSE #100
		SetDialog("RYNN")
		StartDialogueNoSet(Player1)
END

IF
	ActionListEmpty()
THEN
	RESPONSE #100
		ChangeAIScript("RYNNNPC",CLASS)
		ChangeAIScript("",OVERRIDE)
		ChangeAIScript("",DEFAULT)
		ChangeAIScript("",GENERAL)
		ChangeAIScript("",RACE)
		SetDialog("RYNN")
		Continue()
END

IF
Global("CB_RynnWithPC","GLOBAL",1)
CombatCounter(0)
Delay(6)
THEN
RESPONSE #100
MoveToObjectFollow(Protagonist)
END

IF
HPLT(Myself,11)
Global("CB_RynnWithPC","GLOBAL",1)
THEN
RESPONSE #100
ReallyForceSpellRES("BGSleep",Myself) 
Wait(3)
ReallyForceSpell(Myself,HEAL_NO_VISUAL) 
END

IF
	AttackedBy([GOODCUTOFF],DEFAULT)
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Enemy()
END

IF
	Allegiance(Myself,ENEMY)
	Global("CbValiantEquips","LOCALS",0)
THEN
	RESPONSE #100
                ApplySpellRES("CBHLVLNT",Myself)
		EquipMostDamagingMelee()
		SetGlobal("CbValiantEquips","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
	AttackedBy([ANYONE],DEFAULT)
THEN
	RESPONSE #100
		Attack(LastAttackerOf(Myself))
END
